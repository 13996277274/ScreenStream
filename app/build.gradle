apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-parcelize"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"

android {
    compileSdkVersion(30)
    buildToolsVersion("30.0.3")

    defaultConfig {
        applicationId = "info.dvkr.screenstream"
        minSdkVersion(21)
        targetSdkVersion(30)
        versionCode = 30705
        versionName = "3.7.5"
        resConfigs("en", "ru", "pt-rBR", "zh-rTW", "fr-rFR", "fa", "it", "pl", "hi", "de", "sk", "es", "ar", "ja", "gl", "ca")

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        getByName("debug") {
            minifyEnabled = false
            shrinkResources = false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
        }
        getByName("release") {
            minifyEnabled = true
            shrinkResources = true
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")

            packagingOptions {
                exclude("DebugProbesKt.bin")
            }
        }
    }

    Properties localProps = new Properties()
    File localProperties = new File(rootProject.rootDir, "local.properties")
    if (localProperties.exists() && localProperties.isFile()) {
        localProperties.withInputStream { localProps.load(it) }
    }
    flavorDimensions("default")
    productFlavors {
        create("firebase") {
            manifestPlaceholders = ["adMobPubId": "${localProps.getProperty("ad.pubId")}"]
            buildConfigField "String", "AD_UNIT_ID", localProps.getProperty("ad.unitId", "")
        }
        create("firebasefree") {
            manifestPlaceholders = ["adMobPubId": ""]
            firebaseCrashlytics.mappingFileUploadEnabled = false
        }
    }

    buildFeatures.viewBinding = true

    compileOptions {
        coreLibraryDesugaringEnabled = true
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"

        firebase.java.srcDirs += "src/firebase/kotlin"
        firebaseDebug.java.srcDirs += "src/firebaseDebug/kotlin"
        firebaseRelease.java.srcDirs += "src/firebaseRelease/kotlin"

        firebasefree.java.srcDirs += "src/firebasefree/kotlin"
        firebasefreeDebug.java.srcDirs += "src/firebasefreeDebug/kotlin"
        firebasefreeRelease.java.srcDirs += "src/firebasefreeRelease/kotlin"
    }

    packagingOptions {
        exclude("META-INF/INDEX.LIST")
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:1.1.5")

    implementation(project(":data"))

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1")

    implementation("androidx.core:core-ktx:1.6.0")
    implementation("androidx.activity:activity-ktx:1.3.0")
    implementation("androidx.fragment:fragment-ktx:1.4.0-alpha05")
    implementation("androidx.appcompat:appcompat:1.3.1")
    implementation("androidx.constraintlayout:constraintlayout:2.0.4")
    implementation("androidx.recyclerview:recyclerview:1.2.1")
    implementation("com.google.android.material:material:1.4.0")

    implementation("androidx.navigation:navigation-fragment-ktx:2.4.0-alpha05")
    implementation("androidx.navigation:navigation-ui-ktx:2.4.0-alpha05")
    implementation("androidx.lifecycle:lifecycle-livedata:2.3.1")
    implementation("androidx.lifecycle:lifecycle-common-java8:2.3.1")

    implementation("com.afollestad.material-dialogs:core:3.3.0")
    implementation("com.afollestad.material-dialogs:color:3.3.0")
    implementation("com.afollestad.material-dialogs:input:3.3.0")
    implementation("com.afollestad.material-dialogs:lifecycle:3.3.0")
    // Temp fix for https://github.com/afollestad/material-dialogs/issues/1825
    implementation(fileTree("libs/bottomsheets-release.aar"))
//    implementation("com.afollestad.material-dialogs:bottomsheets:3.3.0")

    implementation("io.insert-koin:koin-android:3.1.2")
    implementation("com.github.iamironz:binaryprefs:1.0.1")
    implementation("com.elvishew:xlog:1.11.0")

    firebaseImplementation("com.google.android.play:core:1.10.0")
    firebaseImplementation("com.google.android.play:core-ktx:1.8.1") {
        exclude group: "org.jetbrains.kotlin"
        exclude group: "org.jetbrains.kotlinx"
        exclude group: "androidx.fragment"
        exclude group: "androidx.core"
    }
    firebaseImplementation("com.google.firebase:firebase-analytics:19.0.0")
    firebaseImplementation("com.google.firebase:firebase-crashlytics:18.2.0")
    firebaseImplementation("com.google.android.gms:play-services-ads:20.2.0")

    debugImplementation("com.squareup.leakcanary:leakcanary-android:2.7")
}

android.applicationVariants.all {
    if (flavorName.contains("firebasefree")) {
        project.tasks.getByName("injectCrashlyticsMappingFileId" + name.capitalize()).enabled = false
        project.tasks.getByName("process" + name.capitalize() + "GoogleServices").enabled = false
    }
}